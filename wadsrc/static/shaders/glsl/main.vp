// NOTE: NO_FOG is only for the colormap shader which doesn't use any lighting effects.

#ifndef NO_FOG
	varying vec4 pixelpos;
	attribute vec4 fogparams;
	varying vec4 fogparm;
#endif
#ifndef NO_GLOW
	varying vec2 glowdist;
	attribute vec2 glowdistance;
#endif

attribute float lightlevel_in;
varying float lightlevel;

uniform mat4 ModelMatrix;
uniform mat4 ViewMatrix;
uniform mat4 ProjectionMatrix;
uniform mat4 TextureMatrix;

void main()
{
	lightlevel = lightlevel_in;

	vec4 worldcoord = ModelMatrix * gl_Vertex;
	vec4 eyeCoordPos = ViewMatrix * worldcoord;

	gl_FrontColor = gl_Color;

	#ifndef NO_FOG
		pixelpos.xyz = worldcoord.xyz;
		pixelpos.w = -eyeCoordPos.z/eyeCoordPos.w;
		fogparm = fogparams;
	#endif
	
	#ifndef NO_GLOW
		glowdist = glowdistance;
	#endif
	
	#ifdef SPHEREMAP
		vec3 u = normalize(eyeCoordPos.xyz);
		vec3 n = normalize(gl_NormalMatrix * gl_Normal);
		vec3 r = reflect(u, n);
		float m = 2.0 * sqrt( r.x*r.x + r.y*r.y + (r.z+1.0)*(r.z+1.0) );
		vec2 sst = vec2(r.x/m + 0.5,  r.y/m + 0.5);
		gl_TexCoord[0].xy = sst;
	#else
		gl_TexCoord[0] = TextureMatrix * gl_MultiTexCoord0;
	#endif
	
	
	gl_Position = ProjectionMatrix * eyeCoordPos;
    #ifdef __GLSL_CG_DATA_TYPES
		gl_ClipVertex = eyeCoordPos;
	#endif
}
